plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
    id 'me.shedaniel.unified-publishing' version "${unified_publishing_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        runs {
            data {
                data()

                programArgs "--all", "--mod", "classicui"
                programArgs "--output", file('../src/generated/resources').absolutePath
                programArgs "--existing", file('../src/main/resources').absolutePath
            }
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    common(project(path: ':', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':', configuration: 'transformProductionForge')
}

unifiedPublishing {
    project {
        displayName = "Classic UI Forge ${rootProject.minecraft_version}-${rootProject.mod_version}"
        version = publishVersion
        gameVersions = [rootProject.minecraft_version]
        gameLoaders = ['forge']
        releaseType = 'release'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/ClassicUI'

        mainPublication tasks.remapJar
        secondaryPublication tasks.remapSourcesJar.getArchiveFile()

        curseforge {
            token = Objects.requireNonNullElse(System.getenv('CURSEFORGE_TOKEN'), "")
            id = rootProject.curseforge_project_id
        }

        modrinth {
            token = Objects.requireNonNullElse(System.getenv('MODRINTH_TOKEN'), "")
            id = rootProject.modrinth_project_id
        }
    }
}
