plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
    id 'me.shedaniel.unified-publishing' version "${unified_publishing_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        runs {
            data {
                data()

                programArgs "--all", "--mod", "classicui"
                programArgs "--output", file('../src/generated/resources').absolutePath
                programArgs "--existing", file('../src/main/resources').absolutePath
            }
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
}

shadowJar {
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
}

unifiedPublishing {
    project {
        displayName = "Classic UI Forge ${project.minecraft_version}-${project.mod_version}"
        version = publishVersion
        gameVersions = [project.minecraft_version]
        gameLoaders = ['forge']
        releaseType = 'release'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/ClassicUI'

        mainPublication tasks.remapJar
        secondaryPublication tasks.remapSourcesJar.getArchiveFile()

        curseforge {
            token = System.getenv('CURSEFORGE_TOKEN')
            id = project.curseforge_project_id
        }

        modrinth {
            token = System.getenv('MODRINTH_TOKEN')
            id = project.modrinth_project_id
        }
    }
}
